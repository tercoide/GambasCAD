' Gambas class file

' Gambas class file

' GambasCAD
' A simple CAD made in Gambas
'
' Copyright (C) Ing Martin P Cristia
'
' This program is free software; you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation; either version 3 of the License, or
' (at your option) any later version.
'
' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.
'
' You should have received a copy of the GNU General Public License
' along with this program; if not, write to the Free Software
' Foundation, Inc., 51 Franklin St, Fifth Floor,
' Boston, MA  02110-1301  USA
Create Static
Inherits Caddimension

Public Const Gender As String = "DIMENSION_ANG3PT"
Public Const DrawingOrder As Integer = 100 ' 1 a 32.724 esto debe Drawse primero y ser sobreescrito por el resto
Public Const PointsToDraw As Integer = 1             ' minimal point requered for drawing something usefull
Public Const CmdLineHelper As String = ("Insert a linear acotation")
Public Const ParamType As String = "PPPP"                             ' that is Point, Point, Float; could be Color Text, etc
Public Const ParamHelper As String = "Center of arc;First point;Secont point;extension"

Public Sub ExportDXFCol(e As Entity, ByRef stxExport As String[]) As Boolean

    cadDimension.ExportDXFCol(e, stxExport)

    ' sobreescribo el nombre de la entidad

    stxExport.insert(["AcDb3PointAngularDimension", "100"])
    ' ===========================================================================
    stxExport.insert([CStr(e.p[16]), "13"])                ' first def point
    stxExport.insert([CStr(e.p[17]), "23"])
    stxExport.insert(["0", "33"])

    stxExport.insert([CStr(e.p[18]), "14"])                ' text att point
    stxExport.insert([CStr(e.p[19]), "24"])
    stxExport.insert(["0", "34"])

    stxExport.insert([CStr(e.p[20]), "15"])                ' text att point
    stxExport.insert([CStr(e.p[21]), "25"])
    stxExport.insert(["0", "35"])

    If e.iParam[Me.iiiMode] = 1 Then
        stxExport.insert([CStr(e.p[22]), "16"])                ' text att point
        stxExport.insert([CStr(e.p[23]), "26"])
        stxExport.insert(["0", "36"])

    Endif

End

Public Function ImportDXF(e As Entity, sClaves As String[], sValues As String[]) As Boolean

    Dim i As Integer
    Dim AfterSubclassmarker As Boolean

    For i = 0 To sClaves.Max
        If sClaves[i] = "100" And sValues[i] = "AcDb3PointAngularDimension" Then AfterSubclassmarker = True
        If AfterSubclassmarker Then ' lo siguientes codigos

            If sClaves[i] = "13" Then Me.DefPoint1.x = CFloat(sValues[i])
            If sClaves[i] = "23" Then Me.DefPoint1.y = CFloat(sValues[i])

            If sClaves[i] = "14" Then Me.DefPoint2.x = CFloat(sValues[i])
            If sClaves[i] = "24" Then Me.DefPoint2.y = CFloat(sValues[i])

            If sClaves[i] = "15" Then Me.DefPointCentral.x = CFloat(sValues[i])
            If sClaves[i] = "25" Then Me.DefPointCentral.y = CFloat(sValues[i])

        End If

    Next

    ' TODO: cuando este terminada la entidad, sacar eso
    ' tengo q presuponer que las escalas son = 1
    e.fParam[Me.ipaEscX] = 1    ' esc X
    e.fParam[Me.ipaEscY] = 1    ' esc Y
    e.fParam[Me.ipaEscZ] = 1    ' esc Z
    e.pBlock.AbsoluteCoords = True

    ' y poner esto
    ' e.pBlock = Me.ReBuildBlock(e)

    Return True

    ' catch

    Return False

End

Public Function NewParameter(eBuild As Entity, vParam As Variant[], Optional Definitive As Boolean = False) As Boolean

    ' la linea solo recibe puntos

    ' hay dos modos:
    '   0  se marca el centro del arco a medir y dos puntos
    '   1  se marcan dos puntos de una recta y otros dos puntos de otra recta

    ' luego se marca la posicion del texto
    ' https://ezdxf.readthedocs.io/en/stable/tutorials/angular_dimension.html

    If vParam[0] <> "point" Then Return False

    If gcd.StepsDone = 0 Then
        Me.DimType = gcd.dimAligned

        Try eBuild.P[0] = vParam[1]
        Try eBuild.P[1] = vParam[2]
        eBuild.P[2] = eBuild.P[0]
        eBuild.P[3] = eBuild.P[1]
        If definitive Then Return True
    Else If gcd.StepsDone = 1 Then

        Try eBuild.P[2] = vParam[1]
        Try eBuild.P[3] = vParam[2]
        eBuild.P[4] = eBuild.P[2]
        eBuild.P[5] = eBuild.P[3]

        eBuild.pBlock = ReBuildBlock2(eBuild)
        If definitive Then Return True
    Else If gcd.StepsDone = 2 Then

        Try eBuild.P[4] = vParam[1]
        Try eBuild.P[5] = vParam[2]
        eBuild.P[6] = eBuild.P[4]
        eBuild.P[7] = eBuild.P[5]

        eBuild.pBlock = ReBuildBlock2(eBuild)
        If definitive Then Return True
    Else If gcd.StepsDone = 3 Then

        Try eBuild.P[6] = vParam[1]
        Try eBuild.P[7] = vParam[2]

        eBuild.pBlock = ReBuildBlock2(eBuild)
        If definitive Then Return True

    Endif

End

'' Esta rutina se usa para Angulos y Largo del arco

Public Sub ReBuildBlock2(E As Entity) As Block

    ' esta rutina no sabe si el bloque a reconstruir es desde ENTITYBUILDER o desde
    ' DXF o JSON

    Dim flxTextExt As Float[]
    Dim fRotXY As Float[]

    Dim newBlock As New Block
    Dim f1, f2 As New Float[]
    Dim a1 As Float
    Dim sDim As String
    Dim invertida As Boolean
    Dim d, fAngle90 As Single ' para evitar problemas de mantisa cuando Ang=89.99999

    ' para definir este bloque necesito 6 entidades
    Dim eLineaPpal As Entity
    Dim eOblique1 As Entity       ' tambien pueden ser flechas Solid
    Dim eOblique2 As Entity
    Dim eExtension1 As Entity
    Dim eExtension2 As Entity
    Dim eText As Entity
    Dim eCenter As Entity
    Dim ePoint1 As Entity
    Dim ePoint2 As Entity
    Dim ePoint3 As Entity
    Dim ePoint4 As Entity
    Dim MyStyle As DimStyle
    ' vemos los estilos
    MyStyle = gcd.currentDrawing.CurrDimStyle
    If E.fParam[Me.ipaStyleTextHeigth] > 0 Then Me.DimTextHeight = E.fParam[Me.ipaStyleTextHeigth] Else Me.DimTextHeight = MyStyle.DIMTXT
    If E.fParam[Me.ipaStyleArrowSize] > 0 Then Me.DimArrowSize = E.fParam[Me.ipaStyleArrowSize] Else Me.DimArrowSize = MyStyle.DIMASZ
    Me.DimArrowWidth = Me.DimArrowSize / 3

    newBlock.name = "*D" ' luego se agregara la handle, en entitybuilder
    newBlock.entities = New Collection

    ' Pasamos los puntos que viene marcando el user a nuestros puntos.
    If e.iParam[Me.iiiMode] = 0 Then ' viene con centro
        Me.DefCenter.x = e.P[0]
        Me.DefCenter.y = e.P[1]
        Me.DefPoint1.x = e.P[2]
        Me.DefPoint1.y = e.P[3]
        Me.DefPoint2.x = e.P[4]
        Me.DefPoint2.y = e.P[5]
        Me.DefPointCentral.x = e.P[6]           ' es el punto dnde quiere el arco que demarca el angulo
        Me.DefPointCentral.y = e.P[7]
    Else If e.iParam[Me.iiiMode] = 1 Then  ' viene con dos rectas
        Me.DefCenter.x = e.P[0]
        Me.DefCenter.y = e.P[1]
        Me.DefPoint1.x = e.P[2]
        Me.DefPoint1.y = e.P[3]
        Me.DefPoint2.x = e.P[4]
        Me.DefPoint2.y = e.P[5]
        Me.DefTextMidPoint.x = e.P[6]
        Me.DefTextMidPoint.y = e.P[7]

    End If

    Me.DefAngle = Ang(Me.DefPoint2.x - Me.DefCenter.x, Me.DefPoint2.y - Me.DefCenter.y) - Ang(Me.DefPoint1.x - Me.DefCenter.x, Me.DefPoint1.y - Me.DefCenter.y)

    ' reduzco este valor a -89.99 -> 90
    Me.DefAngle = Utils.fMod(Me.DefAngle, Pi)
    If Me.DefAngle > Pi / 2 Then Me.DefAngle -= Pi
    ' busco el angulo perpendicular
    fAngle90 = Me.DefAngle + Pi / 2
    fAngle90 = Utils.fMod(fAngle90, Pi)
    If fAngle90 > Pi / 2 Then fAngle90 -= Pi

    ' el siguiente valor depende de la posicion del texto
    Me.DefDeltaT = Me.DimTextHeight * 3 / 4

    ' ' los puntos auxiliares, donde van la extension y las flechas, vienen dados por la posicion del texto
    ' d = puntos.distancia(Me.DefCenter.x, Me.DefCenter.y, Me.DefPoint1.x, Me.DefPoint1.y)
    ' If d = 0 Then d = 1
    '
    ' Me.DefPoint1b.x = Me.DefCenter.x + (Me.DefPoint1.x - Me.DefCenter.x) * puntos.distancia(Me.DefCenter.x, Me.DefCenter.y, Me.DefPointCentral.x, Me.DefPointCentral.y) / d
    '
    ' Me.DefPoint1b.y = Me.DefCenter.y + (Me.DefPoint1.y - Me.DefCenter.y) * puntos.distancia(Me.DefCenter.x, Me.DefCenter.y, Me.DefPointCentral.x, Me.DefPointCentral.y) / d
    '
    ' d = puntos.distancia(Me.DefCenter.x, Me.DefCenter.y, Me.DefPoint2.x, Me.DefPoint2.y)
    ' If d = 0 Then d = 1
    '
    ' Me.DefPoint2b.x = Me.DefCenter.x + (Me.DefPoint2.x - Me.DefCenter.x) * puntos.distancia(Me.DefCenter.x, Me.DefCenter.y, Me.DefPointCentral.x, Me.DefTextMidPoint.y) / d
    '
    ' Me.DefPoint2b.y = Me.DefCenter.y + (Me.DefPoint2.y - Me.DefCenter.y) * puntos.distancia(Me.DefCenter.x, Me.DefCenter.y, Me.DefPointCentral.x, Me.DefPointCentral.y) / d

    ' con esto calculo la posicion del texto
    d = puntos.distancia(Me.DefCenter.x, Me.DefCenter.y, Me.DefPointCentral.x, Me.DefPointCentral.y)

    fRotXY = puntos.RotateFromPointWithAngle(Me.DefCenter.x, Me.DefCenter.y, (Ang(Me.DefPoint2.x - Me.DefCenter.x, Me.DefPoint2.y - Me.DefCenter.y) + Ang(Me.DefPoint1.x - Me.DefCenter.x, Me.DefPoint1.y - Me.DefCenter.y)) / 2, Me.DefCenter.x + d, Me.DefCenter.y)
    Me.DefTextMidPoint.x = fRotXY[0]
    Me.DefTextMidPoint.y = fRotXY[1]

    fRotXY = puntos.RotateFromPointWithAngle(Me.DefCenter.x, Me.DefCenter.y, Ang(Me.DefPoint1.x - Me.DefCenter.x, Me.DefPoint1.y - Me.DefCenter.y), Me.DefCenter.x + d, Me.DefCenter.y)
    Me.DefPoint1b.x = fRotXY[0]
    Me.DefPoint1b.y = fRotXY[1]

    fRotXY = puntos.RotateFromPointWithAngle(Me.DefCenter.x, Me.DefCenter.y, Ang(Me.DefPoint2.x - Me.DefCenter.x, Me.DefPoint2.y - Me.DefCenter.y), Me.DefCenter.x + d, Me.DefCenter.y)
    Me.DefPoint2b.x = fRotXY[0]
    Me.DefPoint2b.y = fRotXY[1]

    ' ' ahora recalculo la posicion del terxto
    ' ' el centro de la linea principal
    ' Me.DefPointCentral.x = (Me.DefPoint1b.x + Me.DefPoint2b.x) / 2
    ' Me.DefPointCentral.y = (Me.DefPoint1b.y + Me.DefPoint2b.y) / 2
    ' Me.DefTextMidPoint.x = Me.DefPointCentral.x - Sin(Me.DefAngle) * Me.DefDeltaT
    ' Me.DefTextMidPoint.y = Me.DefPointCentral.y + Cos(Me.DefAngle) * Me.DefDeltaT
    '
    ' Me.DefTextMidPoint.x = (Me.DefPoint1.x + Me.DefPoint2.x) / 2
    ' Me.DefTextMidPoint.y = (Me.DefPoint1.y + Me.DefPoint2.y) / 2

    ' linea principal
    eLineaPpal = cadArc.NewEntity([Me.DefCenter.x, Me.DefCenter.y], True)  ' el 2do punto lo tengo que determinar, el primero esta en la posicion final

    d = puntos.distancia(Me.DefCenter.x, Me.DefCenter.y, Me.DefPointCentral.x, Me.DefPointCentral.y)
    eLineaPpal.fParam[0] = d          ' radio del arco
    eLineaPpal.fParam[1] = Ang(Me.DefPoint1.x - Me.DefCenter.x, Me.DefPoint1.y - Me.DefCenter.y) * 180 / Pi
    eLineaPpal.fParam[2] = Ang(Me.DefPoint2.x - Me.DefCenter.x, Me.DefPoint2.y - Me.DefCenter.y) * 180 / Pi

    If (eLineaPpal.fParam[2] - eLineaPpal.fParam[1]) < 0 Then eLineaPpal.fParam[2] += 360

    ' lineas de demarcacion
    eExtension1 = cadLine.NewEntity([-Me.DimExtension, 0, Me.DimExtension, 0], True)
    eExtension2 = cadLine.NewEntity([-Me.DimExtension, 0, Me.DimExtension, 0], True)
    clsEntities.Rotate(eExtension1, eLineaPpal.fParam[1] / 180 * Pi)
    clsEntities.Rotate(eExtension2, eLineaPpal.fParam[2] / 180 * Pi)

    clsEntities.Translate(eExtension1, Me.DefPoint1b.x, Me.DefPoint1b.y)
    clsEntities.Translate(eExtension2, Me.DefPoint2b.x, Me.DefPoint2b.y)

    ' puntos base, ya en las ubicaciones finales (coinciden con los clicks)
    ePoint1 = cadPoint.NewEntity([Me.DefPoint1.x, Me.DefPoint1.y], True)
    ePoint2 = cadPoint.NewEntity([Me.DefPoint2.x, Me.DefPoint2.y], True)

    ' flechas
    eOblique1 = cadInsert.NewEntity([Me.DefPoint1b.x, Me.DefPoint1b.y], True)
    eOblique1.pBlock = clsEntities.FIndBlock("_thick1_arrow", gcd.currentDrawing.oBlocks)
    eOblique1.fParam[cadInsert.ipaEscX] = 1
    eOblique1.fParam[cadInsert.ipaEscY] = 1
    eOblique1.fParam[cadInsert.ipaEscZ] = 1
    eOblique1.fParam[cadInsert.ipaRotacion] = eLineaPpal.fParam[1] + 90

    eOblique2 = cadInsert.NewEntity([Me.DefPoint2b.x, Me.DefPoint2b.y], True)
    eOblique2.pBlock = clsEntities.FIndBlock("_thick2_arrow", gcd.currentDrawing.oBlocks)
    eOblique2.fParam[cadInsert.ipaEscX] = 1
    eOblique2.fParam[cadInsert.ipaEscY] = 1
    eOblique2.fParam[cadInsert.ipaEscZ] = 1
    eOblique2.fParam[cadInsert.ipaRotacion] = eLineaPpal.fParam[2] + 90

    ' texto
    eText = cadMText.NewEntity([Me.DefTextMidPoint.x, Me.DefTextMidPoint.y], True)  ' si leo un archivo, esta es la posicion final
    eText.fParam[cadMText.ipaAttchmPoint] = 8
    eText.fParam[cadMText.ipaTextAngle] = ((eLineaPpal.fParam[2] + eLineaPpal.fParam[1]) / 2 - 90) / 180 * Pi
    eText.fParam[cadMText.ipaTextHeight] = Me.DimTextHeight

    If (eLineaPpal.fParam[2] - eLineaPpal.fParam[1]) < 0 Then eLineaPpal.fParam[2] += 360

    ' La dimension, segun el modo:
    If e.Gender = "DIMENSION_ANG3PT" Then
        E.fParam[cadDimension.ipaRealMeasure] = (eLineaPpal.fParam[2] - eLineaPpal.fParam[1])
    Else ' L = ang/360 * Pi * diam
        E.fParam[cadDimension.ipaRealMeasure] = (eLineaPpal.fParam[2] - eLineaPpal.fParam[1]) * Pi / 360 * puntos.distancia(Me.DefCenter.x, Me.DefCenter.y, Me.DefPoint1.x, Me.DefPoint1.y) * 2
    End If

    ' agrego todo al bloque
    newBlock.entities.Add(eLineaPpal, eLineaPpal.Handle)
    newBlock.entities.Add(eExtension1, eExtension1.Handle)
    newBlock.entities.Add(eExtension2, eExtension2.Handle)
    newBlock.entities.Add(ePoint1, ePoint1.Handle)
    newBlock.entities.Add(ePoint2, ePoint2.Handle)
    newBlock.entities.Add(eOblique1, eOblique1.Handle)
    newBlock.entities.Add(eOblique2, eOblique2.Handle)
    newBlock.entities.Add(eText, eText.Handle)
    newBlock.AbsoluteCoords = True

    ' -> el bloque
    E.fParam[cadInsert.ipaEscX] = 1      ' ScaleX del inserto
    E.fParam[cadInsert.ipaEscY] = 1      ' ScaleY del inserto
    E.fParam[cadInsert.ipaEscZ] = 1      ' ScaleY del inserto
    E.Visible = True

    If E.StringData[Me.sdaTextOverride] <> "" Then
        sDim = E.StringData[Me.sdaTextOverride]
    Else
        sDim = Format(e.fParam[Me.ipaRealMeasure], Me.DimFormat)
    Endif
    ' el texto
    If e.Gender = "DIMENSION_ANG3PT" Then
        eText.StringData[cadMText.sdaText] = "\\A1;" & sDim & "Â°"
    Else If e.Gender = "ARC_DIMENSION" Then
        eText.StringData[cadMText.sdaText] = "\\A1;" & "L=" & sDim
    End If

    ' ' veo si entra el texto
    ' flxTextExt = glPlus.TextExtends(sDim, Me.DimTextHeight)
    ' If flxTextExt[0] * 1.2 > d Then ' no entra
    '     eOblique1.fParam[cadInsert.ipaRotacion] += 180
    '     eOblique2.fParam[cadInsert.ipaRotacion] += 180
    ' End If

    ' guardo los puntos para exportar a DXF

    e.P[10] = Me.DefPoint1.x        '  10 20 30
    e.P[11] = Me.DefPoint1.y

    e.P[12] = Me.DefTextMidPoint.x  '  11 21 31
    e.P[13] = Me.DefTextMidPoint.y

    e.P[14] = 0 ' baseline, no usado   12 22 32
    e.P[15] = 0

    ' ahora depende del modo de creacion, si tengo centro o no
    If e.iParam[Me.iiiMode] = 0 Then ' con centro
        e.P[16] = eExtension1.P[0]         ' 13 23 33     ' endpoint de la extension line
        e.P[17] = eExtension1.P[1]

        e.P[14] = eExtension2.P[0]                      ' 14 24 34' endpoint de la extension line
        e.P[15] = eExtension2.P[1]

        e.P[20] = Me.DefCenter.x         ' 15 25 35
        e.P[21] = Me.DefCenter.y

    Else ' es con dos lineas
        e.P[16] = eExtension1.P[0]         ' 13 23 33     endpoint de la extension line
        e.P[17] = eExtension1.P[1]

        e.P[14] = eExtension1.P[2]         ' 14 24 34      endpoint de la extension line
        e.P[15] = eExtension1.P[3]

        e.P[20] = eExtension2.P[0]         ' 15 25 35   endpoint de la extension line 2
        e.P[21] = eExtension2.P[1]

        e.P[22] = eExtension2.P[2]          ' 16 26 36 endpoint de la extension line 2
        e.P[23] = eExtension2.P[3]

    End If

    Return newBlock

End
