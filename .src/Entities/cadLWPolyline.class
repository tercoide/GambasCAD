' Gambas class file

' GambasCAD
' A simple CAD made in Gambas
'
' Copyright (C) Ing Martin P Cristia
'
' This program is free software; you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation; either version 3 of the License, or
' (at your option) any later version.
'
' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.
'
' You should have received a copy of the GNU General Public License
' along with this program; if not, write to the Free Software
' Foundation, Inc., 51 Franklin St, Fifth Floor,
' Boston, MA  02110-1301  USA
Create Static
' Fast
Inherits EntityBase

Public Const Gender As String = "LWPOLYLINE"
Public Const PointsToDraw As Integer = 0             ' minimal point requered for drawing something usefull
Public Const CmdLineHelper As String = ("LWPOLYLINE")
Public Const ParamType As String = "P+"                             ' that is Point, Point, Float; could be Color Text, etc
Public Const ParamHelper As String = ("Next Point")      ' a little text that is shown at the prompt waiting for user input during build
Public Const ParamDefault As String = "Close/Undo"
Public Const ContextMenu As String = ("Close polyline;_CLOSE;;;Undo last point;_UNDO;;;End in last point;_END;;") ' el menu para el boton derecho, los comandos en mayusculas NO se traducen

Public Const totalpoints As Integer = 1
' iParam helpers
Public Const iTotalParams As Integer = 2
Public Const iiiPolylineClosed As Integer = 0
Public Const iiiBulged As Integer = 1

Public iContext As Integer = 0 ' var local para usar Draw2 con Draw

' The entity handler receives a user action, and returns the number of expected parameter
' If definitive = true, means the parameter is set
Public Function NewParameter(eBuild As Entity, vParam As Variant[], Optional Definitive As Boolean = False) As Boolean

    If vParam[0] = "point" Then

        eBuild.P[eBuild.P.Max - 1] = vParam[1]
        eBuild.P[eBuild.P.Max] = vParam[2]

        If definitive Then
            eBuild.P.Insert(eBuild.P.Copy(eBuild.P.Max - 1, 2))
            Return True
        End If
    Else If vParam[0] = "text"

        If vParam[1] = "_CLOSE" Then
            If eBuild.P.Count > 4 Then eBuild.P.Remove(eBuild.P.Max - 1, 2)
            eBuild.P.Add(eBuild.P[0])
            eBuild.P.Add(eBuild.P[1])
            eBuild.iParam[0] = 1
            Inc gcd.StepsDone
            Return True

        Else If vParam[1] = "_UNDO" Then
            If eBuild.P.Count > 4 Then eBuild.P.Remove(eBuild.P.Max - 1, 2)
            eBuild.iParam[0] = 0
            Return True
        Else If vParam[1] = "_END" Then
            If eBuild.P.Count > 4 Then eBuild.P.Remove(eBuild.P.Max - 1, 2)
            Inc gcd.StepsDone
            eBuild.iParam[0] = 0
            Return True

        Endif
    End If

End

Public Sub Draw(oE As Entity)

    If Not oE.Visible Then Return

    ' version con bulges
    Dim ang1, mx, my, B, bx, by, Lt As Float
    Dim i As Integer
    Dim fArcParams As Float[]

    'If oe.Handle = "1C1CE090" Then Stop

    If oe.iParam[Me.iiiBulged] Then
        For i = 0 To oe.P.Count / 2 - 2

            ' no todos los tramos pueden tener bulges
            If Abs(oe.fParam[i]) > 0.001 Then
                ang1 = Ang(oe.P[(i + 1) * 2] - oe.P[i * 2], oe.P[(i + 1) * 2 + 1] - oe.P[i * 2 + 1])    ' angulo del tramo
                Lt = puntos.distancia(oe.P[i * 2], oe.P[i * 2 + 1], oe.P[(i + 1) * 2], oe.P[(i + 1) * 2 + 1])
                If Lt = 0 Then Continue
                mx = (oe.P[(i + 1) * 2] + oe.P[i * 2]) / 2          ' punto medio del tramo
                my = (oe.P[(i + 1) * 2 + 1] + oe.P[i * 2 + 1]) / 2
                B = oe.fParam[i] * Lt / 2
                bx = mx + B * Sin(ang1)                             ' Tercer punto del Bulge
                by = my - B * Cos(ang1)

                ' aqui podria usar una rutina de arco entre 3 puntos
                Try fArcParams = puntos.Arc3Point(oe.P[i * 2], oe.P[i * 2 + 1], bx, by, oe.P[(i + 1) * 2], oe.P[(i + 1) * 2 + 1])
                ' verifico Start_Angle y End_Angle (esta parte esta intimamente ligada a Arc3Point)
                ' las combinaciones de angulos son diversas, Arc3Point puede no funcionar bien en otra circunstancia

                ' la condicion a cumplir es que ambos valores sean positivos y que 3<4
                If fArcParams.Count < 5 Then Continue
                ' FIXME: hay bulges que se hace al reves
                'If (oe.fParam[i] < 0) And (fArcParams[3] > fArcParams[4]) Then Swap fArcParams[3], fArcParams[4]
                ' If (oe.fParam[i] < 0) Then Swap fArcParams[3], fArcParams[4]
                If (oe.fParam[i] > 0) Then Swap fArcParams[3], fArcParams[4]
                'If (fArcParams[3] < 0) And (oe.fParam[i] > 0) Then Swap fArcParams[3], fArcParams[4]

                If fArcParams[3] < 0 Then fArcParams[3] += 2 * Pi
                If fArcParams[4] < 0 Then fArcParams[4] += 2 * Pi

                If fArcParams[3] > fArcParams[4] Then fArcParams[4] += 2 * Pi

                If iContext = 0 Then
                    ' traslado el centro
                    gl.Translatef(fArcParams[0], fArcParams[1], 0)

                    glx.ARC(fArcParams[2], fArcParams[3], fArcParams[4] - fArcParams[3], gcd.GetGBColor(oE.Colour, oe.pLayer), gcd.GetLineWt(oE.LineWIdth, oe.pLayer), oE.LineType.TrameLength)

                    gl.Translatef(-fArcParams[0], -fArcParams[1], 0)
                Else If iContext = 1 Then
                    Paint.ARC(fArcParams[0], fArcParams[1], fArcParams[2], fArcParams[3], fArcParams[4] - fArcParams[3])
                End If

                fArcParams = Null

            Else    ' dibujo la linea normalmente
                If iContext = 0 Then
                    glx.DrawLines([oe.P[i * 2], oe.P[i * 2 + 1], oe.P[(i + 1) * 2], oe.P[(i + 1) * 2 + 1]], gcd.GetGBColor(oE.Colour, oe.pLayer), gcd.GetLineWt(oE.LineWIdth, oe.pLayer), oE.LineType.TrameLength)
                Else If iContext = 1 Then
                    paintPlus.Lines([oe.P[i * 2], oe.P[i * 2 + 1], oe.P[(i + 1) * 2], oe.P[(i + 1) * 2 + 1]])
                End If
            End If

        Next
    Else
        If iContext = 0 Then

            glx.PolyLines(oE.p, gcd.GetGBColor(oE.Colour, oe.pLayer), gcd.GetLineWt(oE.LineWIdth, oe.pLayer), oe.LineType.TrameLength)
        Else If iContext = 1 Then
            paintPlus.LineStripe(oe.p)

        End If

    End If

End

Public Sub DrawSelected(oE As Entity)

    glx.PolyLines(oE.P, Color.Gradient(gcd.GetGBColor(oE.Colour, oe.pLayer), Config.BackgroundColor), gcd.GetLineWt(oE.LineWIdth, oe.pLayer), oE.LineType.TrameLength)

End

'' A fat version of the line
Public Sub DrawRemark(oE As Entity)

    If Not oE.Visible Then Return

    glx.PolyLines(oE.P, Color.Gradient(gcd.GetGBColor(oE.Colour, oe.pLayer), Config.WhiteAndBlack), gcd.GetLineWt(oE.LineWIdth, oe.pLayer) + 1, oE.LineType.TrameLength)

End

Public Function Draw2(oE As Entity)

    iContext = 1
    Me.Draw(oe)
    iContext = 0

End
' '' Creo los puntos de interes
' '' Build point of interest
'
' Public Function BuildPOI(oE As Entity) As Integer
'
'     ' si este valor no es provisto , estos POI son de la ultima entidad
'     Dim i As Integer
'
'     oe.PoiPoints.Clear
'     oe.PoiType.Clear
'
'     oe.poiPoints.insert([oE.P[0], oE.P[1]])
'     oe.poiType.Add(gcd.poiEndPoint)
'     'oe.poiEntities.Add(arrIndex)
'
'     For i = 0 To oE.P.count - 4 Step 2
'
'         oe.poiPoints.insert([(oE.P[i + 0] + oE.P[i + 2]) / 2, (oE.P[i + 1] + oE.P[i + 3]) / 2])   ' mid
'         oe.poiType.Add(gcd.poiMIdPoint)
'         'oe.poiEntities.Add(arrIndex)
'
'         oe.poiPoints.insert([oE.P[i + 2], oE.P[i + 3]])
'         oe.poiType.Add(gcd.poiEndPoint)
'         'oe.poiEntities.Add(arrIndex)
'
'         ' line
'         'gcd.CurrDrawing.poiLines.insert([oE.p[i], oE.p[i + 1], oE.p[i + 2], oE.p[i + 3]])
'         'gcd.CurrDrawing.poiLinesEntities.Add(arrIndex)
'         ' gcd.CurrDrawing.poiLinesRelPoint.Insert([i / 2, i / 2 + 1])
'
'     Next
'
' End

' This function will return False if the element couldn't be created for some reason: bad or incomple user input
' True means all is good, False will instruct the clsENTITYBUILDER to delete it
Public Sub Finish(oE As Entity) As Boolean

    'Print "Finish en EntityBase"
    'CreatePolygon(oe)

    If oe.fParam.Count = 0 Then oe.fParam.Add(0)
    '  BuildPOI(oe)

    ' busco el tipo de linea, si hace falta

    ' tareas finales de armado del objeto

    oE.psel.resize(oE.p.count / 2)

End

Public Sub ExportDXFCol(e As Entity, ByRef stxExport As String[]) As Boolean

    Dim i, IMAX As Integer

    ' stxExport.insert(["LWPOLYLINE", dxf.codEntity])
    ' Los datos comunes a todas las entidades son guardados por la rutina que llama a esta
    stxExport.insert(["AcDbPolyline", "100"])
    stxExport.insert([e.P.Count / 2, "90"])              ' n puntos
    stxExport.insert([CStr(e.iParam[Me.iiiPolylineClosed]), "70"])      ' abierta =1 o cerrada =0
    stxExport.insert(["0", "43"])
    imax = e.P.max
    If e.iParam[Me.iiiPolylineClosed] Then imax -= 2
    For i = 0 To e.p.Max Step 2
        stxExport.insert([CStr(e.p[i]), dxf.codX0])
        stxExport.insert([CStr(e.p[i + 1]), dxf.codY0])

        If i / 2 <= e.fParam.max Then stxExport.insert([CStr(e.fParam[i / 2]), "42"])
    Next

End

Public Function ImportDXF(e As Entity, sClaves As String[], sValues As String[]) As Boolean

    Dim i As Integer
    Dim FirstBulge As Boolean
    Dim NewTrame As Boolean
    Dim BulgeAdded As Boolean

    e.P.Clear
    For i = 0 To sClaves.Max

        'If sClaves[i] = "90" Then mPolyLine.vertexcount = Val(sValues[i])
        'If sClaves[i] = "70" Then e.fParam[Me.ipaPolylineClosed] = CInt(sValues[i])       ' 0 = abierta, 1 = cerrada
        If sClaves[i] = "70" Then e.iParam[Me.iiiPolylineClosed] = CInt(sValues[i])        ' 0 = abierta, 1 = cerrada
        If sClaves[i] = "10" Then
            e.p.Add(CFloat(sValues[i]))

            e.fParam.Add(0) ' faux bulge

        End If
        If sClaves[i] = "20" Then e.p.Add(CFloat(sValues[i]))

        If sClaves[i] = "42" Then
            e.iParam[Me.iiiBulged] = 1
            e.fParam[E.fParam.Max] = CFloat(sValues[i])

        End If
    Next

    If e.iParam[Me.iiiPolylineClosed] And e.p.Count > 0 Then ' agrego el primer punto de nuevo
        e.p.Add(e.p[0])
        e.p.Add(e.p[1])
    End If

    Return True

    ' catch

    Return False

End

'' Return if that position is over the entity within the tolerance
Public Function OverMe(e As Entity, Xr As Float, Yr As Float, tolerance As Float) As Boolean

    Dim i As Integer

    For i = 0 To E.P.count - 4 Step 2
        If puntos.doIntersect(xr - tolerance, Yr, Xr + tolerance, Yr, E.P[i + 0], E.P[i + 1], E.P[i + 2], E.P[i + 3]) Or puntos.doIntersect(xr, Yr - tolerance, Xr, Yr + tolerance, E.P[i + 0], E.P[i + 1], E.P[i + 2], E.P[i + 3]) Then Return True
    Next

    Return False

End

Public Function GripEdit(g As Grip) As Boolean

    Dim e As Entity

    e = g.AsociatedEntity

    e.P[g.AsociatedPoint * 2] = g.X
    e.P[g.AsociatedPoint * 2 + 1] = g.Y

    If g.AsociatedPoint = 0 Then
        If e.iParam[Me.iiiPolylineClosed] Then
            e.P[e.P.Max - 1] = g.X
            e.P[e.P.Max] = g.Y

        Endif
    Else If g.AsociatedPoint = e.P.Max Then
        If e.iParam[Me.iiiPolylineClosed] Then
            e.P[0] = g.X
            e.P[1] = g.Y

        Endif
    Endif

End
