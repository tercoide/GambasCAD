' Gambas class file

' GauchoCAD
' A simple CAD made in Gambas
'
' Copyright (C) Ing Martin P Cristia
'
' This program is free software; you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation; either version 3 of the License, or
' (at your option) any later version.
'
' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.
'
' You should have received a copy of the GNU General Public License
' along with this program; if not, write to the Free Software
' Foundation, Inc., 51 Franklin St, Fifth Floor,
' Boston, MA  02110-1301  USA
Create Static
' Fast

Public Const Gender As String = "LWPolyline"
Public Const PointsToDraw As Integer = 0             ' minimal point requered for drawing something usefull
Public Const HelperTrabajo As String = ("LWPolyline")
Public Const ParamType As String = "P+"                             ' that is Point, Point, Float; could be Color Text, etc
Public Const ParamHelper As String = ("Next Point")      ' a little text that is shown at the prompt waiting for user input during build
Public Const ParamDefault As String = "Close/Undo"
Public Const GAMBASCADENTITY As String = "EntityBase"
Public Const USEWITH As String = "EntityBuilder"
Public Const DrawingOrder As Integer = 0        ' 1 = draws first
Public Const OrthogonalIgnored As Boolean = False
Public Const OrthogonalForced As Boolean = False
Public Const StretchAble As Boolean = True           ' Si lo puedo deformar

Public Selected As Boolean

Public DrawAble As Boolean = False          ' True si durante la construccion ya tiene suficientes elementos para poder hacer una representacion digna
Public Visible As Boolean = True            ' si esta entidad puede ser mostrada independientmente
Public Finished As Boolean = False          ' colocar en true cuando se llegue a completar
Public StepsDone As Integer                 ' esto lo cambia cls ElementBuilder durante la construccion

' otras variables articulares

Private Const isOpen As Boolean = False

' JSON and DXF Read/Export
Public Const JSON_Reading As Boolean = True
Public Const JSON_Export As Boolean = False

Public Const DXF_Reading As Boolean = True
Public Const DXF_Export As Boolean = True

' fParam helpers
Public Const TotalParams As Integer = 1
Public Const ipaPolylineClosed As Integer = 0


' The entity handler receives a user action, and returns the number of expected parameter
' If definitive = true, means the parameter is set
Public Function NewParameter(eBuild As Entity, vParam As Variant[], Optional Definitive As Boolean = False) As Boolean

    If eBuild.fParam.Count = 0 Then eBuild.fParam.Add(0)

    If vParam[0] = "point" Then
        eBuild.DrawAble = True

        eBuild.P[eBuild.P.Max - 1] = vParam[1]
        eBuild.P[eBuild.P.Max] = vParam[2]

        If definitive Then
            eBuild.P.Insert(eBuild.P.Copy(eBuild.P.Max - 1, 2))
            Return True
        End If
    Else If vParam[0] = "close" Then
        If eBuild.P.Count > 4 Then eBuild.P.Remove(eBuild.P.Max - 1, 2)
        eBuild.fParam[0] = 1
        Return True

    Else If vParam[0] = "undo" Then
        If eBuild.P.Count > 4 Then eBuild.P.Remove(eBuild.P.Max - 1, 2)
        eBuild.fParam[0] = 0
        Return True

    Endif




End

Public Sub Translate(e As Entity, dX As Float, dY As Float, Optional OnlySelected As Boolean = False)

    Dim i As Integer

    If OnlySelected Then

        For i = 0 To e.Psel.Max
            If e.Psel[i] Then

                e.P[i * 2] += dx
                e.P[i * 2 + 1] += dy

            Endif
        Next

    Else

        puntos.Translate(e.p, dx, dy)
    Endif

End

Public Sub Rotate(e As Entity, radians As Float)

    puntos.Rotate(e.p, radians)

End

Public Sub Scale(e As Entity, sX As Float, sY As Float)

    Dim i As Integer

    For i = 0 To e.p.max Step 2

        e.P[i] = e.P[i] * sX

        e.P[i + 1] = e.P[i + 1] * sY

    Next

End

Public Sub Draw(oE As Entity)

    If Not oE.Visible Then Return

    oE.ColorToPaint = gcd.GetGBColor(oE.colour, oE.Layerid)

    If oe.fParam.Count = 0 Then oe.fParam.Add(0)

    GLplus.PolyLines(oE.p, oE.ColorToPaint, oE.LineWidth, oe.linetype, oe.fParam[Me.ipaPolylineClosed])

End

Public Sub DrawSelected(oE As Entity)

    GLplus.PolyLines(oE.P, gcd.flgWindowBackColor, oE.LineWidth, gcd.stiDashed, oe.fParam[Me.ipaPolylineClosed])
    GLplus.PolyLines(oE.P, gcd.ColorForSelected, oE.LineWidth, oE.LineType, oe.fParam[Me.ipaPolylineClosed])

End

'' A fat version of the line
Public Sub DrawRemark(oE As Entity)

    If Not oE.Visible Then Return

    '  GLplus.PolyLines(oE.p, gcd.flgWindowBackColor, oE.LineWidth, gcd.stiDashed)
    '  GLplus.PolyLines(oE.p, oE.ColorToPaint, oE.LineWidth + 2, oE.LineType)
    If oe.Selected Then
        GLplus.PolyLines(oE.P, gcd.flgWindowBackColor, oE.LineWidth + 2, gcd.stiDashed)
        GLplus.PolyLines(oE.P, gcd.ColorForSelected, oE.LineWidth + 2, oE.LineType)
    Else
        GLplus.PolyLines(oE.P, gcd.flgWindowBackColor, oE.LineWidth, gcd.stiDashed)
        GLplus.PolyLines(oE.P, oE.ColorToPaint, oE.LineWidth + 2, oE.LineType)
    Endif

End

Public Function Draw2(oE As Entity)

    Dim i As Integer

    If Not oE.Visible Then Return

    paint.LineWidth = oE.LineWidth ' / gcd.CurrDrawing.ScaleZoom / gcd.ScaleLines

    If oE.Selected And Not gcd.ToolActive Then
        paint.brush = Paint.Color(gcd.ColorForSelected)
    Else
        paint.brush = Paint.Color(oE.ColorToPaint)
    Endif

    paint.moveto(oE.p[0], oE.p[1])
    For i = 2 To oE.P.Max Step 2
        paint.lineto(oE.p[i], oE.p[i + 1])

    Next

    paint.Stroke

End

'' Creo los puntos de interes
'' Build point of interest

Public Function BuildPOI(oE As Entity, Optional arrIndex As Integer = -1) As Integer

    If arrIndex < 0 Then arrIndex = gcd.CurrDrawing.arrEntities.Max       ' si este valor no es provisto , estos POI son de la ultima entidad
    Dim i As Integer
    oe.PoiPoints.Clear
    oe.PoiType.Clear

    oe.poiPoints.insert([oE.P[0], oE.P[1]])
    oe.poiType.Add(gcd.poiEndPoint)
    'oe.poiEntities.Add(arrIndex)

    For i = 0 To oE.P.count - 4 Step 2

        oe.poiPoints.insert([(oE.P[i + 0] + oE.P[i + 2]) / 2, (oE.P[i + 1] + oE.P[i + 3]) / 2])   ' mid
        oe.poiType.Add(gcd.poiMidPoint)
        'oe.poiEntities.Add(arrIndex)

        oe.poiPoints.insert([oE.P[i + 2], oE.P[i + 3]])
        oe.poiType.Add(gcd.poiEndPoint)
        'oe.poiEntities.Add(arrIndex)

        ' line
        'gcd.CurrDrawing.poiLines.insert([oE.p[i], oE.p[i + 1], oE.p[i + 2], oE.p[i + 3]])
        'gcd.CurrDrawing.poiLinesEntities.Add(arrIndex)
        ' gcd.CurrDrawing.poiLinesRelPoint.Insert([i / 2, i / 2 + 1])

    Next

End

' This function will return False if the element couldn't be created for some reason: bad or incomple user input
' True means all is good, False will instruct the clsEntityBuilder to delete it
Public Sub Finish(oE As Entity) As Boolean

    'Print "Finish en EntityBase"
    'CreatePolygon(oe)

    If oe.fParam.Count = 0 Then oe.fParam.Add(0)
    BuildPOI(oe)

    ' busco el tipo de linea, si hace falta
    If oE.LineTypeName = "Dashed" Then oE.LineType = 1
    ' tareas finales de armado del objeto

    oE.psel.resize(oE.p.count / 2)

    oE.DrawAble = True
    oE.Selected = False
    oE.Finished = True

End

' Importo los datos que puedo procesar de una coleccion y genero un Entity para ser agregado a un array
Public Function ImportJsonCol(c As Collection, e As Entity) As Boolean

    Dim i As Integer

    For i = 0 To c["points"].Max
        e.P.Add(c["points"][i][0])
        e.P.Add(c["points"][i][1])
    Next

    ' FIXME: esto indica si es abierta o cerrada
    If c["flag"] > 0 Then e.fParam.Add(1) Else e.fParam.Add(0)

    Return True


Catch

    Return False

End
' "entity": "LWPOLYLINE",
' "index": 400,
' "type": 77,
' "handle": [0, 254701],
' "size": 58,
' "bitsize": 414,
' "ownerhandle": [12, 1, 8, 254693],
' "_subclass": "AcDbEntity",
' "layer": [5, 2, 16846, 16846],
' "preview_exists": 0,
' "entmode": 0,
' "nolinks": 1,
' "color": 256,
' "ltype_scale": 1.0,
' "ltype_flags": 0,
' "plotstyle_flags": 0,
' "invisible": 0,
' "linewt": 29,
' "_subclass": "AcDbPolyline",
' "flag": 4,
' "const_width": 0.00545454545455,
' "points": [
'   [2914.77910786460507, 1640.5656862246592],
'   [2914.81866147450319, 1640.5261326147629]
' ],
' "bulges": [ el panceado de la polilinea, se usan en las nubes de revision por ejemplo,
' es una lista de valores Float, uno por vertice

Public Sub ExportDXFCol(e As Entity, ByRef cExport As Collection) As Boolean

    Dim i As Integer

    cExport.Add("LWPOLYLINE", dxf.codEntity)
    clsEntities.DXFExportCommonCol(e, cExport)
    cExport.Add("AcDbPolyline", "100")
    cExport.Add(e.P.Count, "90")              ' n puntos
    cExport.Add(CStr(e.fParam[0]), "70")      ' abierta =1 o cerrada =0
    cExport.Add("0", "43")
    For i = 0 To e.p.Max Step 2
        cExport.Add(CStr(e.p[i]), dxf.codX0 & "_" & CStr(i / 2))
        cExport.Add(CStr(e.p[i + 1]), dxf.codY0 & "_" & CStr(i / 2))
    Next

End

Public Sub ExportDXF(e As Entity, hFile As File)

    Dim i As Integer

    Print #hFIle, "  0"                       ' Entity type
    Print #hFIle, "LWPOLYLINE"
    clsEntities.DXFExportCommon(e, hFile)
    Print #hFIle, "100"                     ' Subclass marker
    Print #hFIle, "AcDbPolyline"
    Print #hFIle, " 90"                      ' cantidad de puntos
    Print #hFIle, e.p.Count
    Print #hFIle, " 70"                      ' 0 = abierta; 1 = cerrada
    Print #hFIle, "0"
    Print #hFIle, " 43"                      ' constant width  0 = constant
    Print #hFIle, "0"
    For i = 0 To e.p.Max Step 2
        Print #hFIle, " 10"                      ' Xi
        Print #hFIle, CStr(e.p[i])
        Print #hFIle, " 20"                      ' Yi
        Print #hFIle, CStr(e.p[i + 1])
    Next

End

Public Function ImportDXF(e As Entity, sClaves As String[], sValues As String[]) As Boolean

    Dim i As Integer

    For i = 0 To sClaves.Max

        'If sClaves[i] = "90" Then mPolyLine.vertexcount = Val(sValues[i])
        If sClaves[i] = "70" Then e.fParam.Add(CInt(sValues[i]))       ' 0 = abierta, 1 = cerrada
        If sClaves[i] = "10" Then e.p.Add(CFloat(sValues[i]))
        If sClaves[i] = "20" Then e.p.Add(CFloat(sValues[i]))

    Next
    If e.fParam.Count = 0 Then e.fParam.Add(0)
    If e.fParam[0] = 1 And e.p.Count > 0 Then ' agrego el primer punto de nuevo
        e.p.Add(e.p[0])
        e.p.Add(e.p[1])
    End If

    Return True


Catch

    Return False

End
