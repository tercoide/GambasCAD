' Gambas class file

' GambasCAD
' A simple CAD made in Gambas
'
' Copyright (C) Ing Martin P Cristia
'
' This program is free software; you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation; either version 3 of the License, or
' (at your option) any later version.
'
' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.
'
' You should have received a copy of the GNU General Public License
' along with this program; if not, write to the Free Software
' Foundation, Inc., 51 Franklin St, Fifth Floor,
' Boston, MA  02110-1301  USA

' Scale from a certain point
' Tool maintained by Terco
Inherits ToolsBase
Create Static
Public Const Gender As String = "SCALE"

Public ScaleFactor As Float

Public Function Start(Optional ElemToBuild As Variant, Optional _Mode As Integer = 0) As Boolean
    ' Llamada por FCAD al inicio de la aplicacion de esta herramienta

    ' Aprovechamos para establecer los helpers (comentarios de ayuda en pantalla)
    Me.PointsDone = 0
    Me.PointsTotal = 2

    DrawingAids.CmdLineHelper = ("Scale") & " "

    If gcd.Drawing.SelectedEntities.Count = 0 Then                                     ' we have no elements in the selection

        DrawingAids.Helper.texto = DrawingAids.CmdLineHelper & ": " & ("Select entities")

        gcd.clsJobPrevious = Me
        gcd.clsJob = cadSelection

    Else                                                                    ' need the first point

        DrawingAids.Helper.texto = DrawingAids.CmdLineHelper & ": " & ("Base point")
        DrawingAids.CmdLineHelper = DrawingAids.CmdLineHelper & Str(gcd.Drawing.SelectedEntities.count) & " " & ("elements") & ": " & ("Base point")

    Endif
    Me.Active = True
    gcd.ToolActive = True

End


Public Function MouseUp()

    Dim i As Integer, E As Entity

    Inc Me.PointsDone

    If Me.PointsDone = 1 Then ' acabo de marcar el punto base

        ' ya tengo cosas seleccionadas
        DrawingAids.Helper.texto = ("Final point")

        ' ' punto real base
        ' Me.SelStartXr = gcd.Near(gcd.Xreal(mouse.x))
        ' Me.SelStartYr = gcd.Near(gcd.Yreal(mouse.y))

        ' and we save the mouse click
        Me.SelStartX = mouse.x
        Me.SelStartY = mouse.y

        Me.glTranslate[0] = Me.SelStartXr
        Me.glTranslate[1] = Me.SelStartYr
        Me.glTranslate[2] = 0

        gl.PushMatrix
        gl.NewList(gcd.Drawing.GlListSelectedEntities, gl.COMPILE)                      ' genero los graficos dentro de la lista
        gl.Translatef(-Me.SelStartXr, -Me.SelStartYr, 0)
        For Each E In gcd.Drawing.SelectedEntities
            gl.CallList(E.glDrwList)
        Next
        gl.EndList
        gl.PopMatrix

        ' prevenir acciones tontas como hacer click en otro elemento antes de terminar con el actual
        ' TODO: reparar -> gcd.CurrDrawing.DesHabilitarBotones
    Else If Me.PointsDone = 2 Then ' acabo de marcar el punto destino
        ' permitir otros trabajos
        ' TODO: reparar -> gcd.CurrDrawing.HabilitarBotones

        ' we have the point set from MouseMove
        Me.SelEndX = mouse.x
        Me.SelEndY = mouse.y

        Scale(gcd.Drawing.SelectedEntities, Me.SelStartXr, Me.SelStartYr, ScaleFactor)

        Me.Finish()

    Endif
    gcd.Redraw

End

Public Function MouseMove()

    Dim X, Y As Float
    ' we are waiting for a point

    X = gcd.Near(gcd.Xreal(mouse.x))
    Y = gcd.Near(gcd.Yreal(mouse.y))

    ' yo soy el responsable de chequear POI
    If Not gcd.flgSearchingPOI Then
        gcd.Drawing.iEntity = clsMouseTracking.CheckPOI(X, Y)


        If (gcd.Drawing.iEntity[2] > 0) Then

            ' esto re enganchado

            X = gcd.Drawing.iEntity[0]
            Y = gcd.Drawing.iEntity[1]
        End If
    End If
    If Me.PointsDone = 0 Then
        Me.SelStartXr = X
        Me.SelStartYr = Y

    Else If Me.PointsDone = 1 Then
        ' we compare Real values from the starting point to the location
        ' represente by the mouse now, taking account that the drawing
        ' could have being moved by the mouse wheel

        ' new Real position is
        Me.SelEndXr = X
        Me.SelEndyr = Y

        '  we need To determine scale factor
        '
        ' lets say is the distance from first click to the current position / 10
        '

        ScaleFactor = (puntos.distancia(mouse.x, mouse.y, Me.SelStartX, Me.SelStartY) - 50) / 50

        Me.glScale[0] = ScaleFactor
        Me.glScale[1] = ScaleFactor
        Me.glScale[2] = ScaleFactor

    End If
    gcd.Redraw

End

Public Sub Scale(cEntities As Collection, dx As Float, dy As Float, ScaleFactor As Float)

    Dim e As Entity

    For Each e In cEntities
        gcd.CCC[e.Gender].Translate(e, -dx, -dy)
        gcd.CCC[e.Gender].Scale(e, ScaleFactor, ScaleFactor)
        gcd.CCC[e.Gender].Translate(e, dx, dy)

    Next

End
