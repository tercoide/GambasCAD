' Gambas class file

Private entyHatch As New Entity[]

Public Sub form_Open()

  Dim sFilename As String

  ComboBox1.Add("Solid")
  For Each sFilename In Dir(Application.Path &/ "patterns", "*.dxf")

    ComboBox1.Add(Left$(sFilename, -4))
  Next

End

Public Sub form_Show()

  ' necesito saber si estoy volviendo

  ComboBox1.Index = gcd.CurrentDrawing.LastHatch

End

Public Sub ComboBox1_Click()

  If ComboBox1.Text = "Solid" Then
    gcd.CurrentDrawing.LastHatch = ComboBox1.Index
    gcd.CurrentDrawing.LastHatchPattern = ComboBox1.Text
    gcd.CurrentDrawing.LastHatchFile = ComboBox1.Text
    DrawingArea1.Refresh

    Return
  Endif

  entyHatch.Clear

  Dxf.LoadFile(Application.Path &/ "patterns" &/ ComboBox1.Text & ".dxf", entyHatch, True, True, True)

  DrawingArea1.Refresh

  gcd.CurrentDrawing.LastHatch = ComboBox1.Index
  gcd.CurrentDrawing.LastHatchFile = Application.Path &/ "patterns" &/ ComboBox1.Text & ".dxf"
  gcd.CurrentDrawing.LastHatchPattern = ComboBox1.Text

End

Public Sub DrawingArea1_Draw()

  Dim entidad As Entity

  If ComboBox1.Text = "Solid" Then
    Paint.Brush = Paint.Color(gcd.gColor[fMain.CurrentColor])
    Paint.MoveTo(0, 0)
    Paint.Rectangle(0, 0, Paint.w, Paint.h)
    Paint.Fill

    Return
  Endif

  Paint.Scale(2, 2)

  For Each entidad In entyHatch

    entidad.ColorToPaint = gcd.flgWhiteAndBlack
    entidad.LineWidth = 1
    entidad.ClassHandler.draw2(entidad)
  Next

End

Public Sub btnCAncel_Click()

  Me.Close(1) ' este codigo le dira que el user cancelo el hatch

End

Public Sub btnAdd_Click()

  ' tengo q permitir al user seleccionar una polilinea para formar el contorno del hatch
  Me.Close(2) ' el codigo 2 le dira a la clase que busque el contorno

End

Public Sub txtScale_KeyPress()

  Try gcd.CurrentDrawing.LastScale = Val(txtScale.Text)

End

Public Sub txtRotation_KeyPress()

  Try gcd.CurrentDrawing.LastAngle = Val(txtRotation.Text)

End

Public Sub btnOK_Click()

  ' cadHatchBuilder.LastHatch = ComboBox1.Index
  ' cadHatchBuilder.LastHatchFile = Application.Path &/ "patterns" &/ ComboBox1.Text & ".dxf"
  gcd.CurrentDrawing.LastAngle = modUtils.CPval(txtRotation.Text)

  gcd.CurrentDrawing.LastScale = modUtils.CPval(txtScale.Text)
  If gcd.CurrentDrawing.LastScale = 0 Then gcd.CurrentDrawing.LastScale = 1

  Me.Close(3)

End
