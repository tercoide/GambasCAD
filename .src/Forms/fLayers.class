' Gambas class file

Public Sub btnCancel_Click()

    Me.Close

End

Public Sub Form_Open()

    ' cargo los Layers

    Dim l As Layer
    Dim i As Integer
    Dim iIcon As Integer

    tblLayers.Rows.Count = gcd.Drawing.oLayers.Count
    tblLayers.Rows.Height = 32
    'name show frozen locked color linewt linetype printable

    tblLayers.Columns.Count = 8
    ' acomodo la alineacion
    tblLayers.Columns[0].Alignment = Align.Left     'name
    tblLayers.Columns[1].Alignment = Align.Center   'visible
    tblLayers.Columns[2].Alignment = Align.Center     'frozen
    tblLayers.Columns[3].Alignment = Align.Center     'frozen
    tblLayers.Columns[4].Alignment = Align.Center     'color
    tblLayers.Columns[5].Alignment = Align.Right     'linewt
    tblLayers.Columns[6].Alignment = Align.Left     'linetype
    tblLayers.Columns[7].Alignment = Align.Center     'printable

    ' acomodo el ancho
    tblLayers.Columns[0].Width = 256    'name
    tblLayers.Columns[1].Width = 64     'visible
    tblLayers.Columns[2].Width = 64     'frozen
    tblLayers.Columns[3].Width = 64     'locked
    tblLayers.Columns[4].Width = 64     'color
    tblLayers.Columns[5].Width = 48     'linewt
    tblLayers.Columns[6].Width = 128     'linetype
    tblLayers.Columns[7].Width = 64     'printable

    ' titulos
    tblLayers.Columns[0].Title = ("Nombre")
    tblLayers.Columns[1].Title = ("Ver")
    tblLayers.Columns[2].Title = ("Congelar")
    tblLayers.Columns[3].Title = ("Bloquear")
    tblLayers.Columns[4].Title = ("Color")
    tblLayers.Columns[5].Title = ("Espesor")
    tblLayers.Columns[6].Title = ("Tipo de linea")
    tblLayers.Columns[7].Title = ("Imprimir")

    For Each l In gcd.Drawing.oLayers

        ' name
        tblLayers[i, 0].Text = l.Name
        tblLayers[i, 0].Tag = l.handle
        If gcd.Drawing.CurrLayer.handle = l.handle Then tblLayers[i, 0].Font.Bold = True Else tblLayers[i, 0].Font.Bold = False

        tblLayers[i, 0].Font.Strikeout = False

        ' show
        If l.Visible Then   ' establecemos la imagen adecuada
            tblLayers[i, 1].Picture = gcd.picVisibleOn
        Else
            tblLayers[i, 1].Picture = gcd.picVisibleOff
        Endif
        tblLayers[i, 1].Tag = l.Visible ' guardamos el estado original

        ' frozen
        If l.Frozen Then
            tblLayers[i, 2].Picture = gcd.picFrozenOn
        Else
            tblLayers[i, 2].Picture = gcd.picFrozenOff
        Endif
        tblLayers[i, 2].Tag = l.frozen ' guardamos el estado original

        ' locked
        If l.Locked Then
            tblLayers[i, 3].Picture = gcd.picLockedOn
        Else
            tblLayers[i, 3].Picture = gcd.picLockedOff
        Endif
        tblLayers[i, 3].Tag = l.locked ' guardamos el estado original

        ' color
        tblLayers[i, 4].Picture = paintPlus.picCirculito(8, gcd.gColor[l.Colour], Color.ButtonForeground)
        tblLayers[i, 4].Tag = l.colour ' guardamos el estado original

        ' linewt
        tblLayers[i, 5].Text = Format(l.LineWt, "0.00")
        tblLayers[i, 5].Tag = l.LineWt ' guardamos el estado original

        ' linetype
        tblLayers[i, 6].Text = l.LineType
        tblLayers[i, 6].Tag = l.LineType ' guardamos el estado original

        ' printable

        If l.Printable Then
            tblLayers[i, 7].Picture = gcd.picPrintOn
        Else
            tblLayers[i, 7].Picture = gcd.picPrintOff
        Endif
        tblLayers[i, 7].Tag = l.Printable ' guardamos el estado original

        Inc i
    Next

End

Public Sub tblLayers_DblClick()

    Dim i As Integer
    Dim iRet As Integer

    i = tblLayers.Row

    Select Case tblLayers.Column
        Case 0, 5  'name , linewt

            tblLayers.Edit

        Case 1  'visible

            If tblLayers[i, 1].Tag = False Then   ' establecemos la imagen adecuada
                tblLayers[i, 1].Picture = gcd.picVisibleOn
                tblLayers[i, 1].Tag = True
            Else
                tblLayers[i, 1].Picture = gcd.picVisibleOff
                tblLayers[i, 1].Tag = False
                If gcd.Drawing.CurrLayer.handle = tblLayers[i, 0].tag Then Message.Warning(("El Layer actual sera invisible"))
            Endif

        Case 2  'frozen
            If tblLayers[i, 2].Tag = False Then   ' establecemos la imagen adecuada
                tblLayers[i, 2].Picture = gcd.picFrozenOn
                tblLayers[i, 2].Tag = True
            Else
                tblLayers[i, 2].Picture = gcd.picFrozenOff
                tblLayers[i, 2].Tag = False
            Endif

        Case 3  ' locked
            If tblLayers[i, 3].Tag = False Then   ' establecemos la imagen adecuada
                tblLayers[i, 3].Picture = gcd.picLockedOn
                tblLayers[i, 3].Tag = True
            Else
                tblLayers[i, 3].Picture = gcd.picLockedOff
                tblLayers[i, 3].Tag = False
            Endif

        Case 4 ' color

            iRet = fColors.ShowModal() - 1
            If iRet >= 0 Then
                ' color
                tblLayers[i, 4].Picture = paintPlus.picCirculito(8, gcd.gColor[iRet], Color.ButtonForeground)
                tblLayers[i, 4].Tag = iRet ' guardamos el estado original

            Endif

        Case 7  ' printable
            If tblLayers[i, 7].Tag = False Then   ' establecemos la imagen adecuada
                tblLayers[i, 7].Picture = gcd.picPrintOn
                tblLayers[i, 7].Tag = True
            Else
                tblLayers[i, 7].Picture = gcd.picPrintOff
                tblLayers[i, 7].Tag = False
            Endif

    End Select

End

Public Sub tblLayers_Save(Row As Integer, Column As Integer, Value As String)

    Dim i As Integer

    If Column = 0 Then
        For i = 0 To tblLayers.Rows.Max
            If tblLayers[i, 0].Text = Value Then
                Message.Error(("Ya existe un Layer con ese nombre"))
                Return
            Endif
        Next
        tblLayers[row, Column].Text = Value
    Else If Column = 5 Then
        Try tblLayers[row, Column].Text = Format(Value, "0.00")
    Endif

End

Public Sub btnCurrent_Click()

    Dim i As Integer

    If tblLayers.Row < 0 Then Return
    ' gcd.Drawing.CurrLayer = gcd.Drawing.oLayers[tblLayers[tblLayers.Row, 0].tag]

    For i = 0 To tblLayers.Rows.Max
        'If tblLayers[i, 0].tag = gcd.Drawing.CurrLayer.handle Then
        If tblLayers.Row = i Then
            ' name
            tblLayers[i, 0].Font.Bold = True
        Else
            tblLayers[i, 0].Font.Bold = False
        End If
    Next
    tblLayers.Refresh

End

Public Sub btnEliminar_Click()

    If tblLayers.Row < 0 Then Return
    If tblLayers[tblLayers.Row, 0].Font.bold Then
        Message.Error(("No se puede eliminar el Layer actual"))
        Return
    Endif

    If gcd.Drawing.oLayers[tblLayers[tblLayers.Row, 0].tag].entities.count > 0 Then
        Message.Error(("No se puede eliminar un Layer que contiene graficos"))
        Return
    Endif

    tblLayers[tblLayers.Row, 0].Font.Strikeout = Not tblLayers[tblLayers.Row, 0].Font.Strikeout

    tblLayers.Refresh

End

Public Sub Button1_Click()

    Dim sName As String
    Dim i As Integer

    sName = InputBox(("Nombre del nuevo Layer"), ("Nuevo Layer"), "")

    If sName = "" Then Return

    ' verifico que no haya otro Layer con el mismo nombre

    For i = 0 To tblLayers.Rows.Max
        If tblLayers[i, 0].Text = sName Then
            Message.Error(("Ya existe un Layer con ese nombre"))
            Return
        Endif
    Next

    tblLayers.Rows.Count += 1
    i = tblLayers.Rows.Max
    tblLayers[i, 0].Text = sName
    tblLayers[i, 0].Tag = ""

    tblLayers[i, 1].Picture = gcd.picVisibleOn
    tblLayers[i, 1].Tag = True

    tblLayers[i, 2].Picture = gcd.picFrozenOff
    tblLayers[i, 2].Tag = False

    tblLayers[i, 3].Picture = gcd.picLockedOff
    tblLayers[i, 3].Tag = False

    ' color
    tblLayers[i, 4].Picture = paintPlus.picCirculito(8, gcd.gColor[0], Color.ButtonForeground)
    tblLayers[i, 4].Tag = 0

    ' linewt
    tblLayers[i, 5].Text = Format(1, "0.00")
    tblLayers[i, 5].Tag = 1

    ' linetype
    tblLayers[i, 6].Text = "CONTINUOUS"
    tblLayers[i, 6].Tag = "CONTINUOUS"

    ' printable
    tblLayers[i, 7].Picture = gcd.picPrintOn
    tblLayers[i, 7].Tag = True

End

Public Sub btnApply_Click()

    Dim i As Integer, l As Layer

    ' intento aplicar los cambios
    For i = 0 To tblLayers.Rows.Max
        ' veo si es nuevo
        If tblLayers[i, 0].Tag = "" Then ' es un Layer nuevo
            l = New Layer
            l.Handle = gcd.NewHandle()

            gcd.Drawing.oLayers.Add(l, l.Handle)
        Else
            l = gcd.Drawing.oLayers[tblLayers[i, 0].Tag]
        End If
        l.Name = tblLayers[i, 0].Text
        l.Visible = tblLayers[i, 1].Tag
        l.Frozen = tblLayers[i, 2].Tag
        l.Locked = tblLayers[i, 3].Tag
        l.Colour = tblLayers[i, 4].Tag
        l.LineWt = tblLayers[i, 5].Tag
        l.LineType = tblLayers[i, 6].Tag
        l.Printable = tblLayers[i, 7].Tag

        If tblLayers[i, 0].Font.Bold Then gcd.Drawing.CurrLayer = l
        If tblLayers[i, 0].Font.Strikeout Then gcd.Drawing.oLayers.Remove(l.handle)

    Next

    gcd.UpdateLayersCombo()
    gcd.Regen()

    Me.Close

End
